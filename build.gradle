buildscript {
    repositories {
        mavenLocal()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

repositories {
    mavenLocal()
    jcenter()
    google()
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.dcendents.android-maven' // 'gralde install' task

android {
    compileSdkVersion 25
    buildToolsVersion "28.0.3"

    // http://stackoverflow.com/questions/35312743/workaround-to-link-a-shared-library-in-debug-mode-with-android-studio-v2
    defaultPublishConfig 'release'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 20
        versionName "0.1.6"

        consumerProguardFiles 'consumer-proguard-rules.pro'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

// http://central.sonatype.org/pages/gradle.html

def prop(String name) {
    project.hasProperty(name) ? project.getProperty(name) : null
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.github.axet"
archivesBaseName = "k2pdfopt"
version = android.defaultConfig.versionName

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: prop('ossrhUsername'), password: prop('ossrhPassword'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: prop('ossrhUsername'), password: prop('ossrhPassword'))
            }

            pom.project {
                name 'android k2pdfopt'
                packaging 'jar'
                description 'android k2pdfopt.'
                url 'https://gitlab.com/axet/android-k2pdfopt'

                scm {
                    connection 'scm:git:https://gitlab.com/axet/android-k2pdfopt'
                    developerConnection 'scm:git:https://gitlab.com/axet/android-k2pdfopt'
                    url 'https://gitlab.com/axet/android-k2pdfopt'
                }

                licenses {
                    license {
                        name 'GNU GENERAL PUBLIC LICENSE 3+'
                        url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'axet'
                        name 'Alexey Kuznetsov'
                        email 'axet@me.com'
                    }
                }
            }
        }
    }
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    api 'com.github.axet:libk2pdfopt:2.50-2'
    testImplementation 'junit:junit:4.12'
}
