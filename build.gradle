buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

repositories {
    jcenter()
    mavenLocal()
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.dcendents.android-maven' // 'gralde install' task

android {
    compileSdkVersion 25
    buildToolsVersion "27.0.1"

    // http://stackoverflow.com/questions/35312743/workaround-to-link-a-shared-library-in-debug-mode-with-android-studio-v2
    defaultPublishConfig 'release'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 8
        versionName "0.0.9"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        classpath += files(variant.javaCompile.classpath.files)
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.github.axet"
archivesBaseName = "k2pdfopt"
version = android.defaultConfig.versionName

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'android k2pdfopt'
                packaging 'jar'
                description 'android k2pdfopt.'
                url 'https://gitlab.com/axet/android-k2pdfopt'

                scm {
                    connection 'scm:git:https://gitlab.com/axet/android-k2pdfopt'
                    developerConnection 'scm:git:https://gitlab.com/axet/android-k2pdfopt'
                    url 'https://gitlab.com/axet/android-k2pdfopt'
                }

                licenses {
                    license {
                        name 'GNU GENERAL PUBLIC LICENSE 3+'
                        url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'axet'
                        name 'Alexey Kuznetsov'
                        email 'axet@me.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.github.axet:libk2pdfopt:2.42-3'
    testCompile 'junit:junit:4.12'
}
